#!./node_modules/coffee-script/bin/coffee
log		= require( "logging" ).from __filename
config		= require "config"
async		= require "async"
cli		= require "cli"
net		= require "net"
dns		= require "native-dns"

log "Using #{config.servers.length} servers ( #{s.address for s in config.servers} );"

current_server_index = 0
pick_dns_server = ( ) ->

	t_index = current_server_index

	# Change the current_server_index appropriately to loop through the
	# servers, round robin style. 
	if current_server_index is config.servers.length-1
		current_server_index = 0
	else
		current_server_index += 1

	log "using #{config.servers[t_index].address}"
	config.servers[t_index]

perform_query = ( ip, cb ) ->
	log "Starting query for ip #{ip}"
	answers = [ ]

	# Get the actual record we're going to be querying..
	_reverse = ""
	for b in ip.split( "." ).reverse()
		_reverse += b + "."
	_reverse += "in-addr.arpa"

	question = dns.Question { "name": _reverse, "type": "PTR" }

	req = dns.Request { "question": question, "server": pick_dns_server( ) }

	req.on "timeout", ( ) ->
		return cb "timeout"

	req.on "message", ( err, answer ) ->
		if err
			return cb err

		answers = [ a.data for a in answer.answer ]

	req.on "end", ( ) ->
		return cb null, answers

	req.send()

queue = async.queue ( ip, cb ) ->
	perform_query ip, ( err, res ) ->
		if err
			return cb err
		log res
		cb null
, 10

queue.drain = ( ) ->
	log "Completely done the queue."

cli.withInput ( line, newline, eof ) ->
	if not eof
		if line isnt "" and net.isIPv4 line
			queue.push line, ( err ) ->
				if err
					log "Couldn't query #{line}: #{err}"
				log "done for #{line}."
	else
		log "done!"
